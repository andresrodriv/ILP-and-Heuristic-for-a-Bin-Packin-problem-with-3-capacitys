reset;
model problem2.mod;
data i_10_1.dat;


problem master;
option solver "cplex";

problem pricing;
option solver "cplex";

#auxiliary parameters
param flag default 0;
param max_iter default 100;

#choice of initial subset of items. Every requests is assigned to one virtual machine
let S:= N;

for{i in 1..N}{
	
	let a[i,i] := 1;

}


printf "\n \t \t \t COLUMN GENERATION \n\n\n" > resultsCGproblem2.txt;
printf"------------------------------------------------ \n" > resultsCGproblem2.txt;

for{counter in 1 .. max_iter} 
{     
	#reset flag
	let flag := 0;
	     
    #solve the current master problem   
	solve master;
	
	#print the optimal objective value of the current master problem 
	printf"\n objective function (virtual machines): %7.6f\n", VM_CG > resultsCGproblem2.txt;
	printf"\n used virtual machines: "> resultsCGproblem2.txt;
	for{VMindex in 1..S}{
		if (l[VMindex] > 0) then{	#if it isn't null
			printf"\n virtual machine %d (%g)", VMindex, l[VMindex] > resultsCGproblem2.txt;
			printf"\t request: "> resultsCGproblem2.txt;
			for{requestindex in 1..N}{
				if(a[requestindex, VMindex] == 1) then{
					printf" %d \t", requestindex > resultsCGproblem2.txt;
				}
			}
		}
	
	}
	printf"\n\n"> resultsCGproblem2.txt;
	
	
	#set the profit of each request equal to
	#the corresponding dual variable of the current master problem	
	for{i in 1..N} {
		if(cover[i].dual <=0) then let pi[i] := 0;
		else{
			let pi[i] := cover[i].dual;
		}
	}
		
	#solve the current pricing problem
	solve pricing;
	
	#print the cluster selected by the current pricing problem	
	printf "\n Candidate new cluster of requests: \t" > resultsCGproblem2.txt;	
	for{i in 1..N}
	{
		if (u[i] = 1) then
			printf "%d  ",i > resultsCGproblem2.txt;
	}
	
	#print the profit of the candidate new cluster
	printf "\n profit: %7.6f \n", profit > resultsCGproblem2.txt;
	
	#check if the candidate new cluster makes the current dual solution infeasible
	if  (profit <= 1) then
	{
		#current master problem solution optimal -> set flag to 0	
		let flag := 0;
		
		printf "\n Did not add cluster variable \n \n" > resultsCGproblem2.txt;
		printf"------------------------------------------------ \n" > resultsCGproblem2.txt;
		
		#terminate the column generation procedure
		break;
	}
	else
	{			
		#add the candidate new cluster (column/variable) to the master problem				
		let S:=S+1;	    			
		for{i in 1..N}
			let a[i,S]:= u[i];
		
		#print the current number of clusters in the master problem
	 	printf "\n Added cluster variable\n"> resultsCGproblem2.txt;
	    printf" S=%d \n", S > resultsCGproblem2.txt;
		printf" \n" > resultsCGproblem2.txt;
		printf"------------------------------------------------ \n" > resultsCGproblem2.txt;
		
		#current master problem solution not optimal -> set flag to 1	
		let flag:= 1;	
			 	
	}
}

#solve the master problem with the final set of clusters (columns/variables)
solve master;

#print the optimal objective function value of the final master problem
printf "\n final master objective: %7.6f \n", VM_CG > resultsCGproblem2.txt;
#print the number of columns (variables) of the final master problem	
printf "\n final number of columns: %7d \n", S > resultsCGproblem2.txt;

#check if the column generation procedure is forced to terminate
#before finding the optimal solution 
if (flag=1) then
	printf"\n Solution is not optimal: increase number of iterations!\n" > resultsCGproblem2.txt;	
else
	printf"\n Solution is optimal!\n" > resultsCGproblem2.txt;	
